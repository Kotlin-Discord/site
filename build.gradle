buildscript {
    repositories {
        maven {
            name = 'KotDis'
            url = 'https://maven.kotlindiscord.com/repository/maven-public/'
        }
    }
}

plugins {
    id 'application'

    id 'com.github.jakemarsden.git-hooks' version '0.0.1'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'io.gitlab.arturbosch.detekt' version '1.8.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.72'
}

import com.github.jengelman.gradle.plugins.shadow.transformers.ServiceFileTransformer

group 'com.kotlindiscord.site'
mainClassName = "io.ktor.server.jetty.EngineMain"
targetCompatibility = 1.11
version '1.0-SNAPSHOT'

repositories {
    maven {
        name = 'KotDis'
        url = 'https://maven.kotlindiscord.com/repository/maven-public/'
    }

    maven {
        name = 'JitPack'  // I wish some developers weren't so dang lazy
        url = 'https://jitpack.io'
    }
}

def ktorVersion = "1.3.2"

dependencies {
    implementation "com.kotlindiscord.database:database:+"
    implementation "com.kotlindiscord.api:client:+"

    implementation "com.github.zensum:ktor-sentry-feature:-SNAPSHOT"

    implementation "io.ktor:ktor-client-apache:$ktorVersion"

    implementation "io.ktor:ktor-pebble:$ktorVersion"
    implementation "io.ktor:ktor-locations:$ktorVersion"
    implementation "io.ktor:ktor-serialization:$ktorVersion"
    implementation "io.ktor:ktor-server-sessions:$ktorVersion"
    implementation "io.ktor:ktor-websockets:$ktorVersion"

    implementation "io.ktor:ktor-server-core:$ktorVersion"
    implementation "io.ktor:ktor-server-jetty:$ktorVersion"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0"

    // Logging dependencies
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'io.github.microutils:kotlin-logging:1.7.9'
    implementation 'io.sentry:sentry-logback:1.7.30'
    implementation 'org.codehaus.groovy:groovy:3.0.4'  // For logback config
}

application {
    mainClassName = "io.ktor.server.jetty.EngineMain"
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

detekt {
    buildUponDefaultConfig = true
    config = files("detekt.yml")
}

gitHooks {
    hooks = ['pre-commit': 'detekt']
}

jar {
    manifest {
        attributes(
                "Main-Class": "io.ktor.server.jetty.EngineMain"
        )
    }
}

task generateBuildInfo() {
    outputs.files "src/main/resources/build.properties"

    doLast {
        new File("src/main/resources/build.properties").text = """
            version=$project.version
        """.stripIndent().strip()
    }
}

processResources {
    dependsOn "generateBuildInfo"

    from "src/main/resources/build.properties"

    from ("build/semantic/") {
        into "static"
    }

    from ("node_modules/jquery/dist/") {
        into "static"
    }
}

shadowJar {
    transform(ServiceFileTransformer) {
        path = 'META-INF/services'
        include 'org.eclipse.jetty.http.HttpFieldPreEncoder'
    }
}
